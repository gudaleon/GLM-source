# 1 "src/aed2_oxygen.F90"
!###############################################################################
!#                                                                             #
!# aed2_oxygen.F90                                                             #
!#                                                                             #
!# Developed by :                                                              #
!#     AquaticEcoDynamics (AED) Group                                          #
!#     School of Earth & Environment                                           #
!# (C) The University of Western Australia                                     #
!#                                                                             #
!# Copyright by the AED-team @ UWA under the GNU Public License - www.gnu.org  #
!#                                                                             #
!#   -----------------------------------------------------------------------   #
!#                                                                             #
!# Created May 2011                                                            #
!#                                                                             #
!###############################################################################

# 1 "include/aed2.h" 1 
!###############################################################################
!#                                                                             #
!# aed2.h                                                                      #
!#                                                                             #
!# Developed by :                                                              #
!#     AquaticEcoDynamics (AED) Group                                          #
!#     School of Earth & Environment                                           #
!# (C) The University of Western Australia                                     #
!#                                                                             #
!# Copyright by the AED-team @ UWA under the GNU Public License - www.gnu.org  #
!#                                                                             #
!###############################################################################







!# aed_phytoplankton constants





!# for aed_geochemistry



!#define MISVAL -9999.












# 45
















# 19 "src/aed2_oxygen.F90" 2 

MODULE aed2_oxygen
!-------------------------------------------------------------------------------
! aed2_oxygen --- oxygen biogeochemical model
!
! The AED module oxygen contains equations that describe exchange of
! oxygen across the air/water interface and sediment flux.
!-------------------------------------------------------------------------------
   USE aed2_core

   USE aed2_util,  ONLY: aed2_gas_piston_velocity, aed2_oxygen_sat

   IMPLICIT NONE

   PRIVATE
!
   PUBLIC aed2_oxygen_data_t
!
   TYPE,extends(aed2_model_data_t) :: aed2_oxygen_data_t
!# Variable identifiers
      INTEGER  :: id_oxy
      INTEGER  :: id_temp, id_salt
      INTEGER  :: id_wind
      INTEGER  :: id_Fsed_oxy
      INTEGER  :: id_oxy_sat !, id_atm_oxy_exch3d
      INTEGER  :: id_atm_oxy_exch
      INTEGER  :: id_sed_oxy

!# Model parameters
      real(8) :: Fsed_oxy,Ksed_oxy,theta_sed_oxy
      LOGICAL  :: use_sed_model

     CONTAINS
         PROCEDURE :: define            => aed2_define_oxygen
         PROCEDURE :: calculate_surface => aed2_calculate_surface_oxygen
         PROCEDURE :: calculate         => aed2_calculate_oxygen
         PROCEDURE :: calculate_benthic => aed2_calculate_benthic_oxygen
!        PROCEDURE :: mobility          => aed2_mobility_oxygen
!        PROCEDURE :: light_extinction  => aed2_light_extinction_oxygen
!        PROCEDURE :: delete            => aed2_delete_oxygen

   END TYPE


!===============================================================================
CONTAINS



!###############################################################################
SUBROUTINE aed2_define_oxygen(data, namlst)
!-------------------------------------------------------------------------------
! Initialise the aed2_oxygen model
!
!  Here, the oxygen namelist is read and te variables exported
!  by the model are registered with AED2.
!-------------------------------------------------------------------------------
!ARGUMENTS
   CLASS (aed2_oxygen_data_t),INTENT(inout) :: data
   INTEGER,INTENT(in) :: namlst

!
!LOCALS
   INTEGER  :: status
   real(8) :: oxy_initial=300.
   real(8) :: Fsed_oxy = 48.0
   real(8) :: Ksed_oxy = 30.0
   real(8) :: theta_sed_oxy = 1.0
   CHARACTER(len=64) :: Fsed_oxy_variable=''

   real(8),PARAMETER :: secs_pr_day = 86400.
   NAMELIST /aed2_oxygen/ oxy_initial,Fsed_oxy,Ksed_oxy,theta_sed_oxy,  &
                         Fsed_oxy_variable
!
!-------------------------------------------------------------------------------
!BEGIN
! Read the namelist
   read(namlst,nml=aed2_oxygen,iostat=status)
   IF (status /= 0) STOP 'Error reading namelist aed2_oxygen'

! Store parameter values in our own derived type
! NB: all rates must be provided in values per day,
! and are converted here to values per second.

   data%Fsed_oxy = Fsed_oxy/secs_pr_day
   data%Ksed_oxy = Ksed_oxy
   data%theta_sed_oxy = theta_sed_oxy
   data%use_sed_model = Fsed_oxy_variable .NE. ''

! Register state variables
   data%id_oxy = aed2_define_variable('oxy','mmol/m**3','oxygen',   &
                                    oxy_initial,minimum=zero_)

! Register link to external pools

   IF (data%use_sed_model) data%id_Fsed_oxy = aed2_locate_global_sheet(Fsed_oxy_variable)

! Register diagnostic variables
   data%id_sed_oxy = aed2_define_sheet_diag_variable(       &
                     'sed_oxy', 'mmol/m**2/d', 'Oxygen sediment flux')

   data%id_atm_oxy_exch = aed2_define_sheet_diag_variable(  &
                     'atm_oxy_exch', 'mmol/m**2/d', 'Oxygen exchange across atm/water interface')

!  data%id_atm_oxy_exch3d = aed2_define_sheet_diag_variable( &
!                    'atm_oxy_exch3d', 'mmol/m**2/d', 'Oxygen exchange across atm/water interface')

   data%id_oxy_sat = aed2_define_diag_variable(                  &
                     'sat', 'mmol/m**2/d', 'Oxygen saturation')

! Register environmental dependencies
   data%id_temp = aed2_locate_global('temperature') ! Temperature (degrees Celsius)
   data%id_salt = aed2_locate_global('salinity') ! Salinity (psu)
!  data%id_pres = aed2_locate_global_sheet('pressure') ! Pressure (dbar = 10 kPa)
   data%id_wind = aed2_locate_global_sheet('wind_speed') ! Wind speed at 10 m above surface (m/s)

END SUBROUTINE aed2_define_oxygen
!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++


!###############################################################################
!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++


!###############################################################################
SUBROUTINE aed2_calculate_surface_oxygen(data,column,layer_idx)
!-------------------------------------------------------------------------------
! Air-water exchange for the aed oxygen model
!-------------------------------------------------------------------------------
!ARGUMENTS
   CLASS (aed2_oxygen_data_t),INTENT(in) :: data
   TYPE (aed2_column_t),INTENT(inout) :: column(:)
   INTEGER,INTENT(in) :: layer_idx
!
!LOCALS
! Environment
   real(8) :: temp, salt, wind

! State
   real(8) :: oxy

! Temporary variables
   real(8) :: oxy_atm_flux = zero_
   real(8) :: Coxy_air = zero_ !Dissolved oxygen in the air phase
   real(8) :: koxy_trans = zero_
   real(8) :: windHt !, Tabs
   real(8) :: f_pres  = 1.0      ! Pressure correction function only applicable at high altitudes
!
!-------------------------------------------------------------------------------
!BEGIN

!Get dependent state variables from physical driver
   temp = column(data%id_temp)%cell(layer_idx)    ! Temperature (degrees Celsius)
   salt = column(data%id_salt)%cell(layer_idx)    ! Salinity (psu)
   wind = column(data%id_wind)%cell_sheet ! Wind speed at 10 m above surface (m/s)
   windHt = 10.

! Retrieve current (local) state variable values.
   oxy = column(data%id_oxy)%cell(layer_idx)! Concentration of oxygen in surface layer

   koxy_trans = aed2_gas_piston_velocity(windHt,wind,temp,salt)

! First get the oxygen concentration in the air phase at interface
! Taken from Riley and Skirrow (1974)
   f_pres = 1.0
   Coxy_air = f_pres * aed2_oxygen_sat(salt,temp)

! Get the oxygen flux
   oxy_atm_flux = koxy_trans * (Coxy_air - oxy)

! Transfer surface exchange value to AED2 (mmmol/m2) converted by driver.
   column(data%id_oxy)%flux_atm = oxy_atm_flux

! Also store oxygen flux across the atm/water interface as diagnostic variable (mmmol/m2).
   column(data%id_atm_oxy_exch)%cell_sheet = oxy_atm_flux
   column(data%id_oxy_sat)%cell(layer_idx) =  Coxy_air

END SUBROUTINE aed2_calculate_surface_oxygen
!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++


!###############################################################################
SUBROUTINE aed2_calculate_oxygen(data,column,layer_idx)
!-------------------------------------------------------------------------------
! Right hand sides of aed2_oxygen model
!-------------------------------------------------------------------------------
!ARGUMENTS
   CLASS(aed2_oxygen_data_t),INTENT(in) :: data
   TYPE (aed2_column_t),INTENT(inout) :: column(:)
   INTEGER,INTENT(in) :: layer_idx
!
!LOCALS
   real(8) :: oxy
   real(8) :: diff_oxy
!  real(8),PARAMETER :: secs_pr_day = 86400.

!-------------------------------------------------------------------------------
!BEGIN

! Retrieve current (local) state variable values.
   oxy = column(data%id_oxy)%cell(layer_idx)! oxygen

! Set temporal derivatives
   diff_oxy = 0.

   column(data%id_oxy)%flux_pel(layer_idx) = column(data%id_oxy)%flux_pel(layer_idx) + (diff_oxy)

! If an externally maintained pool is present, change the pool according

! Export diagnostic variables

END SUBROUTINE aed2_calculate_oxygen
!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++


!###############################################################################
SUBROUTINE aed2_calculate_benthic_oxygen(data,column,layer_idx)
!-------------------------------------------------------------------------------
! Calculate pelagic bottom fluxes and benthic sink and source terms of AED oxygen.
! Everything in units per surface area (not volume!) per time.
!-------------------------------------------------------------------------------
!ARGUMENTS
   CLASS (aed2_oxygen_data_t),INTENT(in) :: data
   TYPE (aed2_column_t),INTENT(inout) :: column(:)
   INTEGER,INTENT(in) :: layer_idx
!
!LOCALS
! Environment
   real(8) :: temp !, layer_ht

! State
   real(8) :: oxy

! Temporary variables
   real(8) :: oxy_flux, Fsed_oxy

! Parameters
   real(8),PARAMETER :: secs_pr_day = 86400.
!
!-------------------------------------------------------------------------------
!BEGIN

! Retrieve current environmental conditions for the bottom pelagic layer.
   temp = column(data%id_temp)%cell(layer_idx) ! local temperature

! Retrieve current (local) state variable values.
   oxy = column(data%id_oxy)%cell(layer_idx)! oxygen

   IF (data%use_sed_model) THEN
       Fsed_oxy = column(data%id_Fsed_oxy)%cell_sheet
   ELSE
       Fsed_oxy = data%Fsed_oxy
   ENDIF

! Sediment flux dependent on oxygen and temperature
   oxy_flux = Fsed_oxy * oxy/(data%Ksed_oxy+oxy) * (data%theta_sed_oxy**(temp-20.0))


! Set bottom fluxes for the pelagic (change per surface area per second)
! Transfer sediment flux value to AED2.
   column(data%id_oxy)%flux_pel(layer_idx) = column(data%id_oxy)%flux_pel(layer_idx) + (oxy_flux)

! Set sink and source terms for the benthos (change per surface area per second)
! Note that this must include the fluxes to and from the pelagic.
!column(data%id_ben_oxy)%flux_ben = column(data%id_ben_oxy)%flux_ben + (-oxy_flux)

! Also store sediment flux as diagnostic variable.
   column(data%id_sed_oxy)%cell_sheet = oxy_flux

END SUBROUTINE aed2_calculate_benthic_oxygen
!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++


END MODULE aed2_oxygen
